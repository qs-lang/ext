
{def> nl: {chr> 10}}
{def> tb: {chr> 9}}
{def> cl: {chr> 58}}
{def> ar: {chr> 62}}
{def> lb> {chr> 123}}
{def> rb> {chr> 125}}

{def> str-ltrim> str>
	{priv> nb> 
		{def> nb> 0}
		{do> i> 0: {strlen: {v>str}}>
			{if: {nb}> => 0>
				{def> nb: {if: {cc: {v>str}: {i}}> ~: {chr: 32}> 0> 1}}
			>}
			{if: {nb}> => 1>
				{cc: {v>str}: {i}}
			>}
		}
	}
}

{def> str-reverse> str>
	{priv> strl>
		{def> strl: {strlen: {v>str}}}
		{do> i> 0: {strl}>
			{cc: {v>str}: {clc: {strl}> -: {i}> -> 1}}
		}
	}
}

{def: str-rtrim> str>
  {str-reverse: {str-ltrim: {str-reverse: {v>str}}}}
}

{def: str-trim> str>
  {str-ltrim: {str-rtrim: {v>str}}}
}

{def> cc>
	{chr: {asc: {v>cc-0}: {cc-1}}}
}

{def> str-split-asc> dest> str> code>
	{arr-rm: {dest}}
	{priv> off> c> adr>
		{def> off> 0}
		{def: {dest}-0>}
		{do> i> 0: {strlen: {v>str}}>
			{def> c: {asc: {v>str}: {i}}}
			{def> adr: {dest}-{off}}
			{if: {c}> =: {code}>
				{def> off: {clc: {off}> +> 1}}
				{def: {dest}-{off}>}
			>
				{def: {adr}: {v: {adr}}{chr: {c}}}
			}
		}
		{def: {dest}-len: {clc: {off}> +> 1}}
	}
}

{def> str-split-chr> dest> str> char>
	{str-split-asc: {dest}: {v>str}: {asc: {char}> 0}}
}

{def> str-substr> str> off> len>
	{do> i> 0: {len}>
		{chr: {asc: {v>str-substr-0}: {clc: {off}> +: {i}}}}
	}
}

{def> str-split> dest> str> delim>
	{arr-rm: {dest}}
	{priv> delimlen> i> off> sub> adr> idx>
		{def> delimlen: {strlen: {delim}}}
		{def> i: {strlen: {v>str}}}
		{def> off> 0}
		{def: {dest}-0>}

		{loop> i> 
			{def> idx: {clc: {strlen: {v>str}}> -: {i}}}
			{def> sub: {str-substr: {v>str}: {idx}: {delimlen}} }

			{if: {sub}> ~: {delim}>
				{def> off: {clc: {off}> +> 1}}
				{def> i: {clc: {i}> -: {delimlen}}}
				{def: {dest}-{off}>}
			>
				{def> adr: {dest}-{off}}
				{def: {adr}: {v: {adr}}{cc: {v>str}: {idx}}}
				{def> i: {clc: {i}> -> 1}}
			}
		}
		{def: {dest}-len: {clc: {off}> +> 1}}
	}
}

{def> str-join> arr> sub>
	{do> i> 0: {v: {arr}-len}>
		{v: {arr}-{i}}
		{if: {clc: {i}> +> 1}> (: {v: {arr}-len}> 
			{sub}
		>}
	}
}

{def> str-replace> str> a> b>
	{str-split> str-replace-buff: {v>str}: {a}}
	{str-join> str-replace-buff: {b}}
	{arr-rm> str-replace-buff}
}

{def> str-index-of> dest> str> sub>
	{arr-rm: {dest}}
	{priv> sublen>
		{def> sublen: {strlen: {sub}}}
		{do> i> 0: {clc: {strlen: {v>str}}> -: {sublen}> +> 1}>
			{if: {str-substr: {v>str}: {i}: {sublen}}> ~: {sub}>
				{def: {dest}-{v: {dest}-len}: {i}}
				{def: {dest}-len: {clc: {v: {dest}-len}> +> 1}}
			>}
		}
	}
}

{def> str-lower> str>
	{do> i> 0: {strlen: {str}}>
		{chr-is-upper: {cc: {v>str}: {i}}>
			{chr: {clc: {asc: {v>str}: {i}}> +> 32}> 0}
		>
			{cc: {v>str}: {i}}
		}
	}
}

{def> str-upper> str>
	{do> i> 0: {strlen: {v>str}}>
		{chr-is-lower: {cc: {v>str}: {i}}>
			{chr: {clc: {asc: {v>str}: {i}}> -> 32}> 0}
		>
			{cc: {v>str}: {i}}
		}
	}
}

{def> str-pad-left> str> amount> with>
  {do: i: 0: {clc: {amount}> -: {strlen: {v>str}}}> {with}}{v>str}
}

{def> str-is-lower-at> str> i>
	{if-in-range: {asc: {v>str}: {i}}> 97> 123> 
		{str-is-lower-at-2}
	> 
		{str-is-lower-at-3}
	}
}

{def> str-is-upper-at> str> i>
	{if-in-range: {asc: {v>str}: {i}}> 65> 91> 
		{str-is-upper-at-2}
	> 
		{str-is-upper-at-3}
	}
}

{def> str-is-digit-at> str> i>
	{if-in-range: {asc: {v>str}: {i}}> 48> 58> 
		{str-is-digit-at-2}
	> 
		{str-is-digit-at-3}
	}
}

{def> str-safe> pstr> c1> c2> c3> c4>
	{priv> code>
		{do> i> 0: {strlen: {v: {pstr}}}>
			{def> code: {asc: {v: {pstr}}: {i}}}
			{if-match: {code}>
				123> {c1}>
				125> {c2}>
				58> {c3}>
				62> {c4}>
				{chr: {code}}
			}
		}
	}
}