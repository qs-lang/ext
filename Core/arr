
{def> arr-do> iter> name> expr>
	{do> arr-do-i> 0: {v: {name}-len}>
		{priv: {iter}>
			{def: {iter}: {v: {name}-{arr-do-i}}}
			{arr-do-2}
		}
	}
}

{def> arr-puts> name>
	{puts: --------- len({name}) = {v: {name}-len} {nl}}
	{do> i> 0: {v: {name}-len}>
		{puts: {name}[{i}] = "{v: {name}-{i}}" {nl}}
	}
}

{def> arr-push> name> item>
	{if: {v: {name}-len}> ~>>
		{def: {name}-len> 0}
	>}
	{def: {name}-{v: {name}-len}: {item}}
	{def: {name}-len: {clc: {v: {name}-len}> +> 1}}
}

{def> arr-pop> name>
	{priv> idx>
		{def> idx: {clc: {v: {name}-len}> -> 1}}
		{v: {name}-{idx}}
		{def: {name}-{idx}>}
		{def: {name}-len: {idx}}
	}
}

{def> arr-find> dest> name> str>
	{arr-rm: {dest}}
	{do> i> 0: {v: {name}-len}>
		{if: {v: {name}-{i}}> ~: {str}>
			{arr-push: {dest}: {i}}
		>}
	}
}

{def> arr-rm> name>
	{do> i> 0: {v: {name}-len}>
		{def: {name}-{i}>}
	}
	{def: {name}-len> 0}
}

{def> arr-def> name>
	{arr-rm: {name}}
	{def: {name}-len: {clc: {arr-def-len}> -> 1}}
	{do> i> 0: {v: {name}-len}>
		{def: {name}-{i}: {v: arr-def-{clc: {i}> +> 1}}}
	}
}

{def> arr-map> name> arg> expr>
	{do> i> 0: {v: {name}-len}>
	 	{priv: {arg}>
			{def: {arg}: {v: {name}-{i}}}
			{def: {name}-{i}: {arr-map-2}}
		}
	}
}

{def> arr-reduce> name> arg1> arg1val> arg2> expr>
	{priv: {arg1}>
		{def: {arg1}: {arg1val}}
		{do> i> 0: {v: {name}-len}>
			{def: {arg2}: {v: {name}-{i}}}
			{def: {arg1}: {arr-reduce-4}}
		}
		{v: {arg1}}
	}
}

{def> arr-filter> name> arg> expr>
	{priv> arr-filter-off: {arg}>
		{do> i> 0: {v: {name}-len}>
			{def: {arg}: {v: {name}-{i}}}
			{if: {arr-filter-2}> ~> 1>
				{def: {name}-{sub: {i}: {arr-filter-off}}: {v: {name}-{i}}}
			>
				{incr> arr-filter-off}
			}
		}
		{def: {name}-len: {sub: {v: {name}-len}: {arr-filter-off}}}
	}
}

{def> arr-range> name> from> to> skip>
	{arr-def: {name}}
	{def> to: {clc: {to}> -: {from}> /: {skip}> +> 1}}
	{do> i> 0: {to}> 
		{def: {name}-{i}: {clc: {i}> *: {skip}> +: {from}}}
	}
	{def: {name}-len: {to}}
}

{def> arr-cat> a> b>
	{arr-do> elem: {b}>
		{arr-push: {a}: {elem}}
	}
}

{def> arr-last> name>
	{v: {name}-{sub: {v: {name}-len}> 1}}
}

{def> arr-has> name> elem>
	{priv> arr-has-t>
		{def> arr-has-t> 1}
		{loop> arr-has-t> 
			{if: {v: {name}-{sub: {arr-has-t}> 1}}> ~: {elem}>
				{if: {arr-has-len}> ]> 3> {arr-has-2}> 1}
				{def> arr-has-t> 0}
			>
				{incr> arr-has-t}
				{if: {arr-has-t}> ): {v: {name}-len}>
					{def> arr-has-t> 0}
					{if: {arr-has-len}> ]> 4> {arr-has-3}> 0}
				>}
			}
		}
	}
}

{def> arr-ref> dict> key> value>
	{arr-has: {dict}-keys: {key}>>
		{arr-push: {dict}-keys: {key}}
	}
	{def: {dict}-{key}: {v> value}}
}
