{use> ./lib/core}

{def> time-gen>
	{arr-def> time-sym> a> A> u> d> w> b> B> m> y> Y> h> H> p> M> s> t> z}
	{priv> form>
		{def> form>'+%a;%A;%u;%d;%w;%b;%B;%m;%y;%Y;%I;%H;%p;%M;%S;%s;%Z'}
		{if: {os}> ~> _WIN32>
			{str-split-asc> time-val: {sys: powershell.exe -command "Get-Date -UFormat {form}"}> 59}
		>}
		{if: {os}> ~> __linux__>
			{str-split-asc> time-val: {sys: date {form}}> 59}
		>}
	}
}

{def> time-conv> sym>
	{arr-find> tval> time-sym: {sym}}
	{if: {sym}> ~> t> 
		{str-split-asc> time-conv-ret: {v: time-val-{tval-0}}> 44}
		{time-conv-ret-0}
	>
		{v: time-val-{tval-0}}
	}
}
	
	

{def> time> str>
	{time-gen}
	{if: {time-len}> => 0>
		{def> str> %d.%m.%Y %H{chr> 58}%M{chr> 58}%s}>}
	{priv> i> 
		{def> i> 0}
		{while> {if-ls: {i}: {strlen: {str}}}>
		{if: {asc: {str}: {i}}> => 37>
			{incr> i}
			{time-conv: {cc: {str}: {i}}}
		>
			{cc: {str}: {i}}
		}

			{incr> i}
		}
	}
	{arr-rm> time-val}
	{arr-rm> time-sym}
}

{def> time-leap-year> year> 
		{if: {mod: {year}> 4}> => 0> 
			{if: {mod: {year}> 100}> => 0> 
				{if: {mod: {year}> 400}> => 0> 1> 0}
			> 1}
		> 0}
}


{def> time-date-val> str> 
	{priv> seq>
		{arr-def> time-date> 0> 0> 0> 0> 0> 0}
		{def> seq> YmdHMs}
		{do> i> 1: {add: {strlen: {str}}> 1}> 
			{str-index-of> date-pos: {seq}: {cc: {str}: {sub: {i}> 1}}}
			{def: time-date-{date-pos-0}: {v: time-date-val-{i}}}
		}
		{str-join> time-date>;}
		{arr-rm> time-date}
	}
}


{def> time-date-to-timestamp> date> 
	{str-split-asc> time-date: {date}> 59}
	{priv> time-sec> time-year-sec> time-month-sec> time-day-sec>
		{def> time-year-sec> 0}
		{do> y> 1970: {time-date-0}> 
			{if: {time-leap-year: {y}}> => 0> 
				{def> time-year-sec: {add: {time-year-sec}> 31536000}}
			>
				{def> time-year-sec: {add: {time-year-sec}> 31622400}}
			}
		}
		{def> time-month-sec> 0}
		{arr-def> time-month-day> 31> 28> 31> 30> 31> 30> 31> 31> 30> 31> 30> 31}
		{do> i> 0: {sub: {time-date-1}> 1}> 
			{def> time-month-sec: {clc: {v: time-month-day-{i}}> *> 86400> +: {time-month-sec}}}
		}

		{if-gt: {time-date-1}> 2> 
			{if: {time-leap-year: {time-date-0}}> => 1>
				{def> time-month-sec: {add: {time-month-sec}> 86400}}
			>}
		>}

		{def> time-day-sec> {clc: {time-date-2}> -> 1> *> 86400}}
		{clc: {time-year-sec}> +: {time-month-sec}> +: {time-day-sec}> +: {mul: {v: time-date-3}> 3600}> +: {mul: {v: time-date-4}> 60}> +: {v: time-date-5}}
	}
}


{def> time-date-from-timestamp> timestamp>
	{arr-def> time-date> 1970> 1> 1> 0> 0> 0}
	{priv> seconds>
		{def> seconds> 31536000}
		{while> {if-gt: {timestamp}: {sub: {seconds}> 1}}> 
			{def> timestamp: {sub: {timestamp}: {seconds}}}
			{incr> time-date-0}
			{if: {time-leap-year: {time-date-0}}> => 0> 
				{def> seconds> 31536000}
			>
				{def> seconds> 31622400}
			}
		}
		
		{arr-def> time-month-day> 31> 28> 31> 30> 31> 30> 31> 31> 30> 31> 30> 31}
		{def> seconds: {mul: {time-month-day-0}> 86400}}
		{while> {if-gt: {timestamp}: {sub: {seconds}> 1}}>
			{if-eq: {time-date-1}> 2> 
				{if: {time-leap-year: {time-date-0}}> => 1>
					{def> seconds: {add: {seconds}> 86400}}
				>}
			>}
			{def> timestamp: {sub: {timestamp}: {seconds}}}
			{incr> time-date-1}
			{def> seconds: {mul: {v: time-month-day-{sub: {time-date-1}> 1}}> 86400}}
		}

		{if: {timestamp}> !> 0>
			{def: time-date-2: {clc: {timestamp}> /> 86400> +> 1}}
			{def> timestamp: {mod: {timestamp}> 86400}}
		>}
		
		{if: {timestamp}> !> 0>
			{def: time-date-3: {clc: {timestamp}> /> 3600}}
			{def> timestamp: {mod: {timestamp}> 3600}}
		>}

		{if: {timestamp}> !> 0>
			{def: time-date-4: {clc: {timestamp}> /> 60}}
			{def> timestamp: {mod: {timestamp}> 60}}
		>}
		{def: time-date-5: {timestamp}}

	}
	{time-date-0};{time-date-1};{time-date-2};{time-date-3};{time-date-4};{time-date-5}
	{arr-rm> time-date}
}

{def> time-clc> date1> op> date2>
	{priv> timestamp> 
		{def> timestamp: {time-date-to-timestamp: {date1}}}
		{arr-def> time-const> 31536000> 2592000> 86400> 3600> 60> 1}
		{str-split-asc> time-date: {date2}> 59}
		{do> i> 0> 6> 
			{def> timestamp: {clc: {timestamp}: {op}: {mul: {v: time-date-{i}}: {v: time-const-{i}}}}}
		}
		{time-date-from-timestamp: {timestamp}}
	}
}

{def> time-date-add> date1> date2> 
	{time-clc: {date1}> +: {date2}}
}

{def> time-date-sub> date1> date2> 
	{time-clc: {date1}> -: {date2}}
}

{def> time-date-days-add> date1> days> 
	{time-clc: {date1}> +: {time-date-val> d: {days}}}
}

{def> time-date-days-sub> date1> days> 
	{time-clc: {date1}> -: {time-date-val> d: {days}}}
}