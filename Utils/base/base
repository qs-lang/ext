{use> ./lib/core}

{def> base-from-dec> base> dec>
	{priv> ret> num>
		{def> num> 0123456789ABCDEF}
		{def> dec: {dec}}
		{loop> dec>
			{def> ret: {cc: {num}: {clc: {dec}> %: {base}}}{ret}}
			{def> dec: {clc: {dec}> /: {base}}}
		}
		{ret}
	}
}

{def> base-to-dec> base> val>
	{priv> ret> num> pow> var> 
		{def> num> 0123456789ABCDEF}
		{def> ret> 0}
		{def> pow> 0}
		{do> i> 0: {strlen: {val}}>
			{def> i: {clc: {strlen: {val}}> -: {i}> -> 1}}
			{str-index-of> var: {num}: {cc: {val}: {i}}}
			{def> ret: {clc: {var-0}> *: {clc-pow: {base}: {pow}}> +: {ret}}}
			{incr> pow}
		}
		{ret}
	}
}

{def> hex-from-bin> bin>
	{priv> num> off> i> t> digit>
		{def> num> 0123456789ABCDEF}
		{def> off: {mod: {strlen: {bin}}> 4}}
		{if: {off}> !> 0>
			{def> off: {clc> 4> -: {off}> +: {strlen: {bin}}}}
			{def> bin: {str-pad-left: {bin}: {off}> 0}}
		>}
		{def> i> 0}
		{def> t> 1}
		{loop> t>
			{if: {i}> ]: {clc: {strlen: {bin}}> -> 4}> {def> t> 0}>}
			{def> digit> 0}
			{do> j> 0> 4> 
				{def> digit: {clc> 2> *: {digit}> +: {cc: {bin}: {add: {i}: {j}}}}}
			}
			{cc: {num}: {digit}}

			{def> i: {clc: {i}> +> 4}}
		}
	}
}

{def> bin-from-dec> dec>
	{base-from-dec> 2: {dec}}
}

{def> oct-from-dec> dec>
	{base-from-dec> 8: {dec}}
}

{def> hex-from-dec> dec>
	{base-from-dec> 16: {dec}}
}

{def> bin-to-dec> bin>
	{base-to-dec> 2: {bin}}
}

{def> oct-to-dec> oct>
	{base-to-dec> 8: {oct}}
}

{def> hex-to-dec> hex>
	{base-to-dec> 16: {hex}}
}
